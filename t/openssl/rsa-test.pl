use strict;
use warnings;

sub runcmds {
  my $cmds = shift;
  for (split /\n/, $cmds) {
    s/^\s*(.*?)\s*$/$1/;
    warn "#### >$_<\n";
    my $rv = system($_);
    die "ERROR (rv = $rv)\n" if $rv;
  }
}

sub doit {

### enc openssl > cryptx
runcmds <<'MARKER';
 openssl rsautl -encrypt -inkey rsakey.pub.pem -pubin -out input.encrypted.rsa -in input.data
MARKER

{
  use Crypt::PK::RSA;
  use File::Slurp 'read_file';
  
  my $pkrsa = Crypt::PK::RSA->new("rsakey.priv.pem");
  my $encfile = read_file("input.encrypted.rsa", binmode=>':raw');
  my $plaintext = $pkrsa->decrypt($encfile, 'v1.5');
  print $plaintext;
}

### enc cryptx > openssl
{
  use Crypt::PK::RSA;
  use File::Slurp 'write_file';
  
  my $plaintext = 'secret message';
  my $pkrsa = Crypt::PK::RSA->new("rsakey.pub.pem");
  my $encrypted = $pkrsa->encrypt($plaintext, 'v1.5');
  write_file("input.encrypted.rsa", {binmode=>':raw'}, $encrypted);
}

runcmds <<'MARKER';
 openssl rsautl -decrypt -inkey rsakey.priv.pem -in input.encrypted.rsa
MARKER

### sign openssl > cryptx
runcmds <<'MARKER';
  openssl dgst -sha1 -sign rsakey.priv.pem -out input.sha1-rsa.sig input.data
MARKER

{
 use Crypt::PK::RSA;
 use Crypt::Digest 'digest_file';
 use File::Slurp 'read_file';
  
 my $pkrsa = Crypt::PK::RSA->new("rsakey.pub.pem");
 my $signature = read_file("input.sha1-rsa.sig", binmode=>':raw');
 my $valid = $pkrsa->verify_hash($signature, digest_file("SHA1", "input.data"), "SHA1", "v1.5");
 print $valid ? "SUCCESS" : "FAILURE";
}

### sign cryptx > openssl
{
 use Crypt::PK::RSA;
 use Crypt::Digest 'digest_file';
 use File::Slurp 'write_file';
  
 my $pkrsa = Crypt::PK::RSA->new("rsakey.priv.pem");
 my $signature = $pkrsa->sign_hash(digest_file("SHA1", "input.data"), "SHA1", "v1.5");
 write_file("input.sha1-rsa.sig", {binmode=>':raw'}, $signature);
}

runcmds <<'MARKER';
 openssl dgst -sha1 -verify rsakey.pub.pem -signature input.sha1-rsa.sig input.data
MARKER

}

### MAIN ###

write_file("input.data", "test-file-content");

### keys generated by cryptx
{
 use Crypt::PK::RSA;
 use File::Slurp 'write_file';
 
 my $pkrsa = Crypt::PK::RSA->new;
 $pkrsa->generate_key(256, 65537);
 write_file("rsakey.pub.der",  {binmode=>':raw'}, $pkrsa->export_key_der('public'));
 write_file("rsakey.priv.der", {binmode=>':raw'}, $pkrsa->export_key_der('private'));
 write_file("rsakey.pub.pem",  $pkrsa->export_key_pem('public_x509'));
 write_file("rsakey.priv.pem", $pkrsa->export_key_pem('private'));
 write_file("rsakey-passwd.priv.pem", $pkrsa->export_key_pem('private', 'secret'));
}

runcmds <<'MARKER';
 openssl rsa -in rsakey.priv.der -text -inform der
 openssl rsa -in rsakey.priv.pem -text
 openssl rsa -in rsakey-passwd.priv.pem -text -inform pem -passin pass:secret
 openssl rsa -in rsakey.pub.der -pubin -text -inform der
 openssl rsa -in rsakey.pub.pem -pubin -text 
MARKER

doit();

### keys generated by openssl

runcmds <<'MARKER';
 openssl genrsa -out rsakey.priv.pem 1024
 openssl rsa -in rsakey.priv.pem -out rsakey.priv.der -outform der
 openssl rsa -in rsakey.priv.pem -out rsakey.pub.pem -pubout
 openssl rsa -in rsakey.priv.pem -out rsakey.pub.der -outform der -pubout
 openssl rsa -in rsakey.priv.pem -passout pass:secret -des3 -out rsakey-passwd.priv.pem
MARKER

{
 use Crypt::PK::RSA;
 use File::Slurp 'write_file';
 
 my $pkrsa = Crypt::PK::RSA->new;
 $pkrsa->import_key("rsakey.pub.der");
 $pkrsa->import_key("rsakey.priv.der");
 $pkrsa->import_key("rsakey.pub.pem");
 $pkrsa->import_key("rsakey.priv.pem");
 $pkrsa->import_key("rsakey-passwd.priv.pem", "secret");
}

doit();

warn "\nSUCCESS\n";