use strict;
use warnings;
use File::Glob 'bsd_glob';

sub runcmds {
  my $cmds = shift;
  for (split /\n/, $cmds) {
    s/^\s*(.*?)\s*$/$1/;
    warn "#### >$_<\n";
    my $rv = system($_);
    die "ERROR (rv = $rv)\n" if $rv;
  }
}

sub doit {

### sign openssl > cryptx
runcmds <<'MARKER';
  openssl dgst -sha1 -sign test_dsakey.priv.pem -out test_input.sha1-dsa.sig test_input.data
MARKER

{
 use Crypt::PK::DSA;
 use Crypt::Digest 'digest_file';
 use Crypt::Misc 'read_rawfile';

 my $pkdsa = Crypt::PK::DSA->new("test_dsakey.pub.pem");
 my $signature = read_rawfile("test_input.sha1-dsa.sig");
 my $valid = $pkdsa->verify_hash($signature, digest_file("SHA1", "test_input.data"));
 print $valid ? "SUCCESS" : "FAILURE";
}

### sign cryptx > openssl
{
 use Crypt::PK::DSA;
 use Crypt::Digest 'digest_file';
 use Crypt::Misc 'write_rawfile';

 my $pkdsa = Crypt::PK::DSA->new("test_dsakey.priv.pem");
 my $signature = $pkdsa->sign_hash(digest_file("SHA1", "test_input.data"));
 write_rawfile("test_input.sha1-dsa.sig", $signature);
}

runcmds <<'MARKER';
 openssl dgst -sha1 -verify test_dsakey.pub.pem -signature test_input.sha1-dsa.sig test_input.data
MARKER

}

### MAIN ###

write_rawfile("test_input.data", "test-file-content");

### keys generated by cryptx
{
 use Crypt::PK::DSA;
 use Crypt::Misc 'write_rawfile';

 my $pkdsa = Crypt::PK::DSA->new;
 $pkdsa->generate_key(20, 128);
 write_rawfile("test_dsakey.pub.der",  $pkdsa->export_key_der('public'));
 write_rawfile("test_dsakey.priv.der", $pkdsa->export_key_der('private'));
 write_rawfile("test_dsakey.pub.pem",  $pkdsa->export_key_pem('public_x509'));
 write_rawfile("test_dsakey.priv.pem", $pkdsa->export_key_pem('private'));
 write_rawfile("test_dsakey-passwd.priv.pem", $pkdsa->export_key_pem('private', 'secret'));
}

runcmds <<'MARKER';
 openssl dsa -in test_dsakey.priv.der -text -inform der
 openssl dsa -in test_dsakey.priv.pem -text
 openssl dsa -in test_dsakey-passwd.priv.pem -text -inform pem -passin pass:secret
 openssl dsa -in test_dsakey.pub.der -pubin -text -inform der
 openssl dsa -in test_dsakey.pub.pem -pubin -text
MARKER

doit();

### keys generated by openssl

runcmds <<'MARKER';
 openssl dsaparam -genkey -out test_dsakey.priv.pem 1024
 openssl dsa -in test_dsakey.priv.pem -out test_dsakey.priv.der -outform der
 openssl dsa -in test_dsakey.priv.pem -out test_dsakey.pub.pem -pubout
 openssl dsa -in test_dsakey.priv.pem -out test_dsakey.pub.der -outform der -pubout
 openssl dsa -in test_dsakey.priv.pem -passout pass:secret -des3 -out test_dsakey-passwd.priv.pem
MARKER

{
 use Crypt::PK::DSA;

 my $pkdsa = Crypt::PK::DSA->new;
 $pkdsa->import_key("test_dsakey.pub.der");
 $pkdsa->import_key("test_dsakey.priv.der");
 $pkdsa->import_key("test_dsakey.pub.pem");
 $pkdsa->import_key("test_dsakey.priv.pem");
 $pkdsa->import_key("test_dsakey-passwd.priv.pem", "secret");
}

doit();

warn "\nSUCCESS\n";
unlink $_ for (bsd_glob("test_*.der"), bsd_glob("test_*.pem"), bsd_glob("test_*.sig"), bsd_glob("test_*.data"));
