use strict;
use warnings;

sub runcmds {
  my $cmds = shift;
  for (split /\n/, $cmds) {
    s/^\s*(.*?)\s*$/$1/;
    warn "#### >$_<\n";
    my $rv = system($_);
    die "ERROR (rv = $rv)\n" if $rv;
  }
}

sub doit {

### sign openssl > cryptx
runcmds <<'MARKER';
  openssl dgst -sha1 -sign dsakey.priv.pem -out input.sha1-dsa.sig input.data
MARKER

{
 use Crypt::PK::DSA;
 use Crypt::Digest 'digest_file';
 use File::Slurp 'read_file';
  
 my $pkdsa = Crypt::PK::DSA->new("dsakey.pub.pem");
 my $signature = read_file("input.sha1-dsa.sig", binmode=>':raw');
 my $valid = $pkdsa->verify_hash($signature, digest_file("SHA1", "input.data"), "SHA1", "v1.5");
 print $valid ? "SUCCESS" : "FAILURE";
}

### sign cryptx > openssl
{
 use Crypt::PK::DSA;
 use Crypt::Digest 'digest_file';
 use File::Slurp 'write_file';
  
 my $pkdsa = Crypt::PK::DSA->new("dsakey.priv.pem");
 my $signature = $pkdsa->sign_hash(digest_file("SHA1", "input.data"), "SHA1", "v1.5");
 write_file("input.sha1-dsa.sig", {binmode=>':raw'}, $signature);
}

runcmds <<'MARKER';
 openssl dgst -sha1 -verify dsakey.pub.pem -signature input.sha1-dsa.sig input.data
MARKER

}

### MAIN ###

write_file("input.data", "test-file-content");

### keys generated by cryptx
{
 use Crypt::PK::DSA;
 use File::Slurp 'write_file';
 
 my $pkdsa = Crypt::PK::DSA->new;
 $pkdsa->generate_key(20, 128);
 write_file("dsakey.pub.der",  {binmode=>':raw'}, $pkdsa->export_key_der('public'));
 write_file("dsakey.priv.der", {binmode=>':raw'}, $pkdsa->export_key_der('private'));
 write_file("dsakey.pub.pem",  $pkdsa->export_key_pem('public_x509'));
 write_file("dsakey.priv.pem", $pkdsa->export_key_pem('private'));
 write_file("dsakey-passwd.priv.pem", $pkdsa->export_key_pem('private', 'secret'));
}

runcmds <<'MARKER';
 openssl dsa -in dsakey.priv.der -text -inform der
 openssl dsa -in dsakey.priv.pem -text
 openssl dsa -in dsakey-passwd.priv.pem -text -inform pem -passin pass:secret
 openssl dsa -in dsakey.pub.der -pubin -text -inform der
 openssl dsa -in dsakey.pub.pem -pubin -text 
MARKER

doit();

### keys generated by openssl

runcmds <<'MARKER';
 openssl dsaparam -genkey -out dsakey.priv.pem 1024
 openssl dsa -in dsakey.priv.pem -out dsakey.priv.der -outform der
 openssl dsa -in dsakey.priv.pem -out dsakey.pub.pem -pubout
 openssl dsa -in dsakey.priv.pem -out dsakey.pub.der -outform der -pubout
 openssl dsa -in dsakey.priv.pem -passout pass:secret -des3 -out dsakey-passwd.priv.pem
MARKER

{
 use Crypt::PK::DSA;
 use File::Slurp 'write_file';
 
 my $pkdsa = Crypt::PK::DSA->new;
 $pkdsa->import_key("dsakey.pub.der");
 $pkdsa->import_key("dsakey.priv.der");
 $pkdsa->import_key("dsakey.pub.pem");
 $pkdsa->import_key("dsakey.priv.pem");
 $pkdsa->import_key("dsakey-passwd.priv.pem", "secret");
}

doit();

warn "\nSUCCESS\n";